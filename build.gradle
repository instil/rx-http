plugins {
    id "com.jfrog.bintray" version "1.2"
    id "java"
    id "idea"
    id "checkstyle"
    id "findbugs"
    id "maven-publish"
}

group = "co.instil.rx"
version = "1.3"

repositories {
    jcenter()
}

dependencies {
    // https://github.com/ReactiveX/RxApacheHttp/pull/5 - the source of rxapache-http is bundled
    // with this module until this pull request is merged to fix chunked encoding
    // compile "io.reactivex:rxapache-http:0.21.0"
    compile "io.reactivex:rxjava:1.0.12"
    compile "io.reactivex:rxjava-string:0.21.0"
    compile "org.apache.httpcomponents:httpasyncclient:4.1"
    compile "org.slf4j:slf4j-api:1.7.2"

    testCompile "junit:junit:4.11"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.mockito:mockito-core:1.9.5"
}

findbugs {
    includeFilter = file("config/findbugs/includeFilter.xml")
    excludeFilter = file("config/findbugs/excludeFilter.xml")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "instil"
            name "Instil"
            email "team@instil.co"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode("description", "A simple wrapper over the top of rxapache-http")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    // bintray_user and bintray_key should be configured in ~/.gradle/gradle.properties
    user = bintray_user
    key = bintray_key
    configurations = ["archives"]
    publications = ["mavenJava"]
    publish = true
    pkg {
        repo = "Instil"
        userOrg = "instil"
        name = "co.instil.rx:rx-http"
        desc = "A simple wrapper over the top of rxapache-http"
        websiteUrl = "https://github.com/instil/rx-http"
        issueTrackerUrl = "https://github.com/instil/rx-http/issues"
        vcsUrl = "https://github.com/instil/rx-http.git"
        licenses = ["Apache-2.0"]
        labels = ["rx-http"]
        publicDownloadNumbers = true
    }
}